include "main.pil";

namespace mem(256);
    // ========= Table MEM-TR =================
    pol commit clk;
    pol commit tsp; // Timestamp derived form clk and sub-operation types (SUB_CLK)
    pol commit addr;
    pol commit space_id;
    pol commit glob_addr;
    pol commit tag; // Memory tag (0: uninitialized, 1: u8, 2: u16, 3: u32, 4: u64, 5: u128, 6:field)
    pol commit val;
    pol commit rw; // Enum: 0 (read), 1 (write)
    pol commit lastAccess; // Boolean (1 when this row is the last of a given address)
    pol commit last; // Boolean indicating the last row of the memory trace (not execution trace)
    pol commit sel_mem; // Selector for every row pertaining to the memory trace
    pol commit sel_rng_chk; // Selector for row on which range-checks apply.

    pol commit r_in_tag; // Instruction memory tag ("foreign key" pointing to main.r_in_tag)
    pol commit w_in_tag; // Instruction memory tag ("foreign key" pointing to main.w_in_tag)
    pol commit skip_check_tag; // A boolean value which relaxes the consistency check in memory
                               // trace between tag and r_in_tag. Required for CMOV opcode.

    // Indicator of the intermediate register pertaining to the memory operation (foreign key to main.sel_mem_op_XXX)
    pol commit sel_op_a;
    pol commit sel_op_b;
    pol commit sel_op_c;
    pol commit sel_op_d;

    // Indicator of the indirect register pertaining to the memory operation (foreign key to main.sel_resolve_ind_addr_XXX)
    pol commit sel_resolve_ind_addr_a;
    pol commit sel_resolve_ind_addr_b;
    pol commit sel_resolve_ind_addr_c;
    pol commit sel_resolve_ind_addr_d;

    // Selector for calldata_copy/return memory operations triggered from memory slice gadget.
    pol commit sel_op_slice;

    // Selectors related to MOV/CMOV opcodes (copied from main trace for loading operation on intermediated register ia/ib)
    // Boolean constraint is performed in main trace.
    pol commit sel_mov_ia_to_ic;
    pol commit sel_mov_ib_to_ic;
    pol commit sel_op_cmov;

    // Error columns
    pol commit tag_err; // Boolean (1 if r_in_tag != tag is detected)

    // Helper columns
    pol commit one_min_inv; // Extra value to prove r_in_tag != tag with error handling
    // pol DIFF: 40-bit difference between two consecutive timestamps or two consecutive addresses
    pol commit diff_hi;  // Higher 8-bit limb of diff.
    pol commit diff_mid; // Middle 16-bit limb of diff.
    pol commit diff_lo;  // Lower 16-bit limb of diff.

    // Type constraints
    lastAccess * (1 - lastAccess) = 0;
    last * (1 - last) = 0;
    rw * (1 - rw) = 0;
    tag_err * (1 - tag_err) = 0;
    sel_op_a * (1 - sel_op_a) = 0;
    sel_op_b * (1 - sel_op_b) = 0;
    sel_op_c * (1 - sel_op_c) = 0;
    sel_op_d * (1 - sel_op_d) = 0;
    sel_op_slice * (1 - sel_op_slice) = 0;
    sel_resolve_ind_addr_a * (1 - sel_resolve_ind_addr_a) = 0;
    sel_resolve_ind_addr_b * (1 - sel_resolve_ind_addr_b) = 0;
    sel_resolve_ind_addr_c * (1 - sel_resolve_ind_addr_c) = 0;
    sel_resolve_ind_addr_d * (1 - sel_resolve_ind_addr_d) = 0;

    // TODO:  1) Ensure that addr is constrained to be 32 bits by the main trace and/or bytecode decomposition
    //        2) Ensure that tag, r_in_tag, w_in_tag are properly constrained by the main trace and/or bytecode decomposition

    // Definition of sel_mem
    sel_mem = sel_op_a + sel_op_b + sel_op_c + sel_op_d
            + sel_resolve_ind_addr_a + sel_resolve_ind_addr_b + sel_resolve_ind_addr_c + sel_resolve_ind_addr_d
            + sel_op_slice;
    // Maximum one memory operation enabled per row
    sel_mem * (sel_mem - 1) = 0; // TODO: might be infered by the main trace

    // Enforce the memory entries to be contiguous, i.e., as soon as
    // sel_mem is disabled all subsequent rows have sel_mem disabled.
    #[MEM_CONTIGUOUS]
    (1 - main.sel_first) * sel_mem' * (1 - sel_mem) = 0;

    // Memory trace rows cannot start at first row
    #[MEM_FIRST_EMPTY]
    main.sel_first * sel_mem = 0;

    // Definition of last, i.e., last row with sel_mem activated
    #[MEM_LAST]
    (1 - last) * sel_mem * (1 - sel_mem') = 0;

    // Definition of sel_rng_chk. It is a boolean as sel_mem and last are booleans.
    sel_rng_chk = sel_mem * (1 - last);

    // sub_clk derivation
    // Current sub_clk range is [0,12) which is subdivided as follows:
    // [0,4): indirect memory operations (read-only resolution of the direct address)
    // [4,8): direct read memory operations
    // [8, 12): direct write memory operations
    // Each sub-range of 4 values correspond to registers ordered as a, b, c, d.

    pol NUM_SUB_CLK = 12;
    pol IND_OP = sel_resolve_ind_addr_a + sel_resolve_ind_addr_b + sel_resolve_ind_addr_c + sel_resolve_ind_addr_d;
    pol SUB_CLK = sel_mem * (sel_resolve_ind_addr_b + sel_op_b + 2 * (sel_resolve_ind_addr_c + sel_op_c) + 3 * (sel_resolve_ind_addr_d + sel_op_d) + 4 * (1 - IND_OP + rw));
    // We need the sel_mem factor as the right factor is not zero when all columns are zero.

    // Calldata_copy memory slice operations will have a sub_clk value of 8 as rw == 1 which is outside of the range of
    // indirect memory operations. This is crucial as a main trace entry for calldata_copy triggers an indirect memory
    // load operation for intermediate register c. The write slice memory operations will have the same sub_clk which in
    // this particular case is not a problem as all addresses are different. Similarly return memory slice operations
    // will have a sub_clk value of 4.

    #[TIMESTAMP]
    tsp = NUM_SUB_CLK * clk + SUB_CLK;

    #[GLOBAL_ADDR]
    glob_addr = space_id * 2**32 + addr;

    #[LAST_ACCESS_FIRST_ROW]
    main.sel_first * (1 - lastAccess) = 0;
    // Remark: lastAccess == 1 on first row and therefore any relation with the
    //         multiplicative term (1 - lastAccess) implicitly includes (1 - main.sel_first)
    //         Similarly, this includes (1 - last) as well.

    // lastAccess == 0 ==> glob_addr' == glob_addr
    // Optimization: We removed the term (1 - main.sel_first)
    #[MEM_LAST_ACCESS_DELIMITER]
    (1 - lastAccess) * (glob_addr' - glob_addr) = 0;
    
    // We need: lastAccess == 1 ==> glob_addr' > glob_addr
    // The above implies: glob_addr' == glob_addr ==> lastAccess == 0
    // This condition does not apply on the last row.
 
    // In addition, we need glob_addr' == glob_addr ==> tsp' > tsp
    // For all rows pertaining to the memory trace (sel_mem == 1) except the last one,
    // i.e., when sel_rng_chk == 1, we compute the difference:
    // 1) glob_addr' - glob_addr if lastAccess == 1
    // 2) tsp' - tsp   if lastAccess == 0 (i.e., whenever glob_addr' == glob_addr)
    pol DIFF = lastAccess * (glob_addr' - glob_addr) + (1 - lastAccess) * (tsp' - tsp);

    // We perform a 40-bit range check of DIFF which proves that glob_addr' > glob_addr if lastAccess == 1
    // and tsp' > tsp whenever glob_addr' == glob_addr
    // Therefore, we ensure proper grouping of each global address and each memory access pertaining to a given
    // global address is sorted according the arrow of time.
    #[DIFF_RNG_CHK_DEC]
    sel_rng_chk * (DIFF - diff_hi * 2**32 - diff_mid * 2**16 - diff_lo) = 0;

    // lastAccess == 0 && rw' == 0 ==> val == val'
    // This condition does not apply on the last row.
    // Note: in barretenberg, a shifted polynomial will be 0 on the last row (shift is not cyclic)
    // Note2: in barretenberg, if a poynomial is shifted, its non-shifted equivalent must be 0 on the first row
    
    // Optimization: We removed the term (1 - main.sel_first) and (1 - last)
    #[MEM_READ_WRITE_VAL_CONSISTENCY]
    (1 - lastAccess) * (1 - rw') * (val' - val) = 0;
    
    // lastAccess == 0 && rw' == 0 ==> tag == tag'
    // Optimization: We removed the term (1 - main.sel_first) and (1 - last)
    #[MEM_READ_WRITE_TAG_CONSISTENCY]
    (1 - lastAccess) * (1 - rw') * (tag' - tag) = 0;

    // Constrain that the first load from a given address has value 0. (Consistency of memory initialization.)
    // We do not constrain that the tag == 0 as the 0 value is compatible with any memory type.
    // As we enforce lastAccess = 1 on the first row, the following condition applies also for the first memory entry:
    #[MEM_ZERO_INIT]
    lastAccess * (1 - rw') * val' = 0;

    // TODO: Verfiy that skip_check_tag cannot be enabled maliciously by the prover.
    // Skip check tag enabled for some MOV/CMOV opcodes and RETURN opcode (sel_op_slice)
    #[SKIP_CHECK_TAG]
    skip_check_tag = sel_op_cmov * (sel_op_d + sel_op_a * (1-sel_mov_ia_to_ic) + sel_op_b * (1-sel_mov_ib_to_ic)) + sel_op_slice;

    // Memory tag consistency check for load operations, i.e., rw == 0.
    // We want to prove that r_in_tag == tag <==> tag_err == 0
    // We want to show that we can invert (r_in_tag - tag) when tag_err == 1,
    // i.e., tag_err == 1 ==> r_in_tag != tag
    // For this purpose, we need an extra column to store a witness
    // which can be used to show that (r_in_tag - tag) is invertible (non-zero).
    // We re-use the same zero (non)-equality technique as in SUBOP_DIVISION_ZERO_ERR1/2 applied
    // to (r_in_tag - tag) by replacing tag_err by 1 - tag_err because here
    // the equality to zero is not an error. Another modification
    // consists in storing 1 - (r_in_tag - tag)^(-1) in the extra witness column
    // instead of (r_in_tag - tag)^(-1) as this allows to store zero by default (i.e., when tag_err == 0).
    // The new column one_min_inv is set to 1 - (r_in_tag - tag)^(-1) when tag_err == 1
    // but must be set to 0 when tags are matching and tag_err = 0
    // Relaxation: This relation is relaxed when skip_check_tag is enabled or for 
    // uninitialized memory, i.e. tag == 0.
    #[MEM_IN_TAG_CONSISTENCY_1]
    tag * (1 - skip_check_tag) * (1 - rw) * ((r_in_tag - tag) * (1 - one_min_inv) - tag_err) = 0;
    // TODO: Try to decrease the degree of the above relation, e.g., skip_check_tag might be consolidated
    // with tag == 0 and rw == 1.
    #[MEM_IN_TAG_CONSISTENCY_2]
    tag * (1 - tag_err) * one_min_inv = 0;

    #[NO_TAG_ERR_WRITE_OR_SKIP]
    (skip_check_tag + rw) * tag_err = 0;

    // Correctness of two above checks MEM_IN_TAG_CONSISTENCY_1/2 (assuming rw == 0):
    // r_in_tag == tag ==> tag_err == 0 (first relation)
    // tag_err == 0 ==> one_min_inv == 0 by second relation. First relation ==> r_in_tag - tag == 0

    // Enforce that a write instruction tag is equal to tag for a write operation
    rw * (w_in_tag - tag) = 0;

    // Enforce that a write instruction never leads to a tag error
    #[NO_TAG_ERR_WRITE]
    rw * tag_err = 0;

    //====== Indirect Memory Constraints =====================================
    // Enforce r_in_tag == 3, i.e., r_in_tag must be U32
    sel_resolve_ind_addr_a * (r_in_tag - 3) = 0;
    sel_resolve_ind_addr_b * (r_in_tag - 3) = 0;
    sel_resolve_ind_addr_c * (r_in_tag - 3) = 0;
    sel_resolve_ind_addr_d * (r_in_tag - 3) = 0;

    // Indirect operation is always a load
    sel_resolve_ind_addr_a * rw = 0;
    sel_resolve_ind_addr_b * rw = 0;
    sel_resolve_ind_addr_c * rw = 0;
    sel_resolve_ind_addr_d * rw = 0;

    //====== CALLDATACOPY/RETURN specific constraints ==================================
    sel_op_slice * (w_in_tag - 6) = 0; // Only write elements of type FF
    sel_op_slice * (r_in_tag - 6) = 0; // Only read elements of type FF

    //====== MOV/CMOV Opcode Tag Constraint =====================================
    // The following constraint ensures that the r_in_tag is set to tag for
    // the load operation pertaining to Ia resp. Ib.
    // The permutation check #[PERM_MAIN_MEM_A/B] guarantees that the r_in_tag
    // value load operation for Ia/Ib is copied back in the main trace.
    // Constraint #[MOV_MAIN_SAME_TAG] copies r_in_tag to w_in_tag in the main
    // trace. Then, #[PERM_MAIN_MEM_C] copies w_in_tag for store operation from Ic.
    #[MOV_SAME_TAG]
    (sel_mov_ia_to_ic + sel_mov_ib_to_ic) * tag_err = 0; // Equivalent to (sel_mov_ia_to_ic + sel_mov_ib_to_ic) * (r_in_tag - tag) = 0