
include "avm_mem.pil";
include "avm_alu.pil";
include "avm_binary.pil";
include "constants.pil";
include "avm_kernel.pil";
include "avm_gas.pil";
include "gadgets/avm_conversion.pil";
include "gadgets/avm_sha256.pil";
include "gadgets/avm_poseidon2.pil";
include "gadgets/avm_keccakf1600.pil";
include "gadgets/avm_pedersen.pil";

namespace avm_main(256);
    //===== CONSTANT POLYNOMIALS ==================================================
    pol constant clk(i) { i };
    pol constant first = [1] + [0]*; // Used mostly to toggle off the first row consisting
                                     // only in first element of shifted polynomials.

    //===== KERNEL INPUTS =========================================================
    // Kernel lookup selector opcodes
    pol commit q_kernel_lookup;

    // CALL CONTEXT
    pol commit sel_op_sender;
    pol commit sel_op_address;
    pol commit sel_op_storage_address;

    // FEES
    pol commit sel_op_fee_per_l2_gas;
    pol commit sel_op_fee_per_da_gas;
    pol commit sel_op_transaction_fee;
    
    // GLOBALS
    pol commit sel_op_chain_id;
    pol commit sel_op_version;
    pol commit sel_op_block_number;
    pol commit sel_op_coinbase;
    pol commit sel_op_timestamp;

    // MACHINE STATE - GAS
    pol commit sel_op_l2gasleft;
    pol commit sel_op_dagasleft;

    //===== KERNEL OUTPUTS ========================================================
    pol commit q_kernel_output_lookup;

    pol commit sel_op_note_hash_exists;
    pol commit sel_op_emit_note_hash;
    pol commit sel_op_nullifier_exists;
    pol commit sel_op_emit_nullifier;
    pol commit sel_op_l1_to_l2_msg_exists;
    pol commit sel_op_emit_unencrypted_log;
    pol commit sel_op_emit_l2_to_l1_msg;
    pol commit sel_op_get_contract_instance;

    pol commit sel_op_sload;
    pol commit sel_op_sstore;

    //===== GAS ACCOUNTING ========================================================
    // 1. The gas lookup table (in avm_gas.pil) is constant and maps the opcode value to L2/DA gas (fixed) cost.
    // 2. Read gas_op from gas table based on the opcode value
    // 3. TODO(#6588): constrain gas start and gas end

    // TODO: Opcode value (byte) will be constrained by the bytecode validation circuit
    pol commit opcode_val;

    // "gas_remaining" values correspond to the gas quantity which remains BEFORE the execution of the opcode
    // of the pertaining line. This means that the last value will be written on the row following the last
    // opcode execution.
    pol commit l2_gas_remaining;
    pol commit da_gas_remaining;

    // TODO: allow lookups to evaluate expressions
    pol commit l2_gas_op;
    pol commit da_gas_op;

    // Boolean indicating whether the current opcode gas consumption is higher than remaining gas
    pol commit l2_out_of_gas;
    pol commit da_out_of_gas;

    // Absolute gas remaining value after the operation in 16-bit high and low limbs
    pol commit abs_l2_rem_gas_hi;
    pol commit abs_l2_rem_gas_lo;
    pol commit abs_da_rem_gas_hi;
    pol commit abs_da_rem_gas_lo;

    // Boolean constraints
    l2_out_of_gas * (1 - l2_out_of_gas) = 0;
    da_out_of_gas * (1 - da_out_of_gas) = 0;

    // Constrain that the gas decrements correctly per instruction
    #[L2_GAS_REMAINING_DECREMENT]
    gas_cost_active * (l2_gas_remaining' - l2_gas_remaining + l2_gas_op) = 0;
    #[DA_GAS_REMAINING_DECREMENT]
    gas_cost_active * (da_gas_remaining' - da_gas_remaining + da_gas_op) = 0;

    // Constrain that the remaining gas is unchanged otherwise (multi-line operations)
    #[L2_GAS_INACTIVE]
    (1 - gas_cost_active) * l2_gas_op = 0;
    #[DA_GAS_INACTIVE]
    (1 - gas_cost_active) * da_gas_op = 0;

    // TODO: constrain that it stays the same if an opcode selector is not active -> TODO: will this break when the opcode takes multiple rows
    // So we also need to constrain that it is the first line of the opcodes execution

    // Prove that l2_out_of_gas == 0 <==> l2_gas_remaining' >= 0
    // Same for da gas
    // TODO: Ensure that remaining gas values are initialized as u32 and that gas l2_gas_op/da_gas_op are u32.
    gas_cost_active * ((1 - 2 * l2_out_of_gas) * l2_gas_remaining' - 2**16 * abs_l2_rem_gas_hi - abs_l2_rem_gas_lo) = 0;
    gas_cost_active * ((1 - 2 * da_out_of_gas) * da_gas_remaining' - 2**16 * abs_da_rem_gas_hi - abs_da_rem_gas_lo) = 0;

    #[LOOKUP_OPCODE_GAS]
    gas_cost_active {opcode_val, l2_gas_op, da_gas_op}
    in
    avm_gas.gas_cost_sel {clk, avm_gas.l2_gas_fixed_table, avm_gas.da_gas_fixed_table};

    #[RANGE_CHECK_L2_GAS_HI]
    gas_cost_active {abs_l2_rem_gas_hi} in sel_rng_16 {clk};

    #[RANGE_CHECK_L2_GAS_LO]
    gas_cost_active {abs_l2_rem_gas_lo} in sel_rng_16 {clk};

    #[RANGE_CHECK_DA_GAS_HI]
    gas_cost_active {abs_da_rem_gas_hi} in sel_rng_16 {clk};

    #[RANGE_CHECK_DA_GAS_LO]
    gas_cost_active {abs_da_rem_gas_lo} in sel_rng_16 {clk};

    //===== Gadget Selectors ======================================================
    pol commit sel_op_radix_le;
    pol commit sel_op_sha256;
    pol commit sel_op_poseidon2;
    pol commit sel_op_keccak;
    pol commit sel_op_pedersen;
    
    //===== Fix Range Checks Selectors=============================================
    // We re-use the clk column for the lookup values of 8-bit resp. 16-bit range check.
    pol commit sel_rng_8;  // Boolean selector for the  8-bit range check lookup
    pol commit sel_rng_16; // Boolean selector for the 16-bit range check lookup

    //===== Lookup table powers of 2 =============================================
    pol commit table_pow_2; // Table of powers of 2 for 8-bit numbers. 

    //===== CONTROL FLOW ==========================================================
    // Program counter
    pol commit pc; 
    // Return Pointer
    pol commit internal_return_ptr;
    // Call Pointer (nested call)
    pol commit call_ptr;
    
    pol commit sel_internal_call;
    pol commit sel_internal_return;
    pol commit sel_jump;
    pol commit sel_jumpi;
    pol commit sel_external_call;
    
    // Halt program execution
    pol commit sel_halt;

    // Memory Space Identifier
    pol commit space_id;

    //===== MEMORY OPCODES ==========================================================
    pol commit sel_mov;
    pol commit sel_cmov;
    
    //===== TABLE SUBOP-TR ========================================================
    // Boolean selectors for (sub-)operations. Only one operation is activated at
    // a time.

    // ADD
    pol commit sel_op_add;
    // SUB
    pol commit sel_op_sub;
    // MUL
    pol commit sel_op_mul;
    // DIV
    pol commit sel_op_div;
    // FDIV
    pol commit sel_op_fdiv;
    // NOT
    pol commit sel_op_not;
    // EQ
    pol commit sel_op_eq;
    // AND
    pol commit sel_op_and;
    // OR
    pol commit sel_op_or;
    // XOR
    pol commit sel_op_xor;
    // CAST
    pol commit sel_op_cast;
    // LT
    pol commit sel_op_lt;
    // LTE
    pol commit sel_op_lte;
    // SHL
    pol commit sel_op_shl;
    // SHR
    pol commit sel_op_shr;

    // Helper selector to characterize an ALU chiplet selector
    pol commit alu_sel;

    // Helper selector to characterize a Binary chiplet selector
    pol commit bin_sel;

    // Instruction memory tags read/write (1: u8, 2: u16, 3: u32, 4: u64, 5: u128, 6: field)
    pol commit r_in_tag;
    pol commit w_in_tag;
    pol commit alu_in_tag; // Copy of r_in_tag or w_in_tag depending of the operation. It is sent to ALU trace.

    // Errors
    pol commit op_err; // Boolean flag pertaining to an operation error
    pol commit tag_err; // Boolean flag (foreign key to avm_mem.tag_err)

    // A helper witness being the inverse of some value
    // to show a non-zero equality
    pol commit inv;
    pol commit id_zero; // Boolean telling whether id is zero (cmov opcode)

    // Intermediate register values
    pol commit ia;
    pol commit ib;
    pol commit ic;
    pol commit id;
    
    // Memory operation selector per intermediate register
    pol commit mem_op_a;
    pol commit mem_op_b;
    pol commit mem_op_c;
    pol commit mem_op_d;

    // Read-write flag per intermediate register: Read = 0, Write = 1
    pol commit rwa;
    pol commit rwb;
    pol commit rwc;
    pol commit rwd;
    
    // Indirect register values
    pol commit ind_a;
    pol commit ind_b;
    pol commit ind_c;
    pol commit ind_d;

    // Indirect memory operation selector per indirect register
    pol commit ind_op_a;
    pol commit ind_op_b;
    pol commit ind_op_c;
    pol commit ind_op_d;

    // Memory index involved into a memory operation per pertaining intermediate register
    // We should range constrain it to 32 bits ultimately. For first version of the AVM,
    // we will assume that these columns are of the right type.
    pol commit mem_idx_a;
    pol commit mem_idx_b;
    pol commit mem_idx_c;
    pol commit mem_idx_d;
  
    // Track the last line of the execution trace. It does NOT correspond to the last row of the whole table
    // of size N. As this depends on the supplied bytecode, this polynomial cannot be constant.
    pol commit last;
    
    // Relations on type constraints
    // TODO: Very likely, we can remove these constraints as the selectors should be derived during
    // opcode decomposition.
    sel_op_sender * (1 - sel_op_sender) = 0;
    sel_op_address * (1 - sel_op_address) = 0;
    sel_op_storage_address * (1 - sel_op_storage_address) = 0;
    sel_op_chain_id * (1 - sel_op_chain_id) = 0;
    sel_op_version * (1 - sel_op_version) = 0;
    sel_op_block_number * (1 - sel_op_block_number) = 0;
    sel_op_coinbase * (1 - sel_op_coinbase) = 0;
    sel_op_timestamp * (1 - sel_op_timestamp) = 0;
    sel_op_fee_per_l2_gas * (1 - sel_op_fee_per_l2_gas) = 0;
    sel_op_fee_per_da_gas * (1 - sel_op_fee_per_da_gas) = 0;
    sel_op_transaction_fee * (1 - sel_op_transaction_fee) = 0;

    // MACHINE STATE - GAS
    sel_op_l2gasleft * (1 - sel_op_l2gasleft) = 0;
    sel_op_dagasleft * (1 - sel_op_dagasleft) = 0;

    sel_op_note_hash_exists * (1 - sel_op_note_hash_exists) = 0;
    sel_op_emit_note_hash * (1 - sel_op_emit_note_hash) = 0;
    sel_op_nullifier_exists * (1 - sel_op_nullifier_exists) = 0;
    sel_op_emit_nullifier * (1 - sel_op_emit_nullifier) = 0;
    sel_op_l1_to_l2_msg_exists * (1 - sel_op_l1_to_l2_msg_exists) = 0;
    sel_op_emit_unencrypted_log * (1 - sel_op_emit_unencrypted_log) = 0;
    sel_op_emit_l2_to_l1_msg * (1 - sel_op_emit_l2_to_l1_msg) = 0;
    sel_op_get_contract_instance * (1 - sel_op_get_contract_instance) = 0;

    sel_op_sload * (1 - sel_op_sload) = 0;
    sel_op_sstore * (1 - sel_op_sstore) = 0;

    sel_op_radix_le * (1 - sel_op_radix_le) = 0;
    sel_op_sha256 * (1 - sel_op_sha256) = 0;
    sel_op_poseidon2 * (1 - sel_op_poseidon2) = 0;
    sel_op_keccak * (1 - sel_op_keccak) = 0;
    sel_op_pedersen * (1 - sel_op_pedersen) = 0;

    sel_op_add * (1 - sel_op_add) = 0;
    sel_op_sub * (1 - sel_op_sub) = 0;
    sel_op_mul * (1 - sel_op_mul) = 0;
    sel_op_div * (1 - sel_op_div) = 0;
    sel_op_fdiv * (1 - sel_op_fdiv) = 0;
    sel_op_not * (1 - sel_op_not) = 0;
    sel_op_eq * (1 - sel_op_eq) = 0;
    sel_op_and * (1 - sel_op_and) = 0;
    sel_op_or * (1 - sel_op_or) = 0;
    sel_op_xor * (1 - sel_op_xor) = 0;
    sel_op_cast * (1 - sel_op_cast) = 0;
    sel_op_lt * (1 - sel_op_lt) = 0;
    sel_op_lte * (1 - sel_op_lte) = 0;
    sel_op_shl * (1 - sel_op_shl) = 0;
    sel_op_shr * (1 - sel_op_shr) = 0;

    sel_internal_call * (1 - sel_internal_call) = 0;
    sel_internal_return * (1 - sel_internal_return) = 0;
    sel_jump * (1 - sel_jump) = 0;
    sel_jumpi * (1 - sel_jumpi) = 0;
    sel_halt * (1 - sel_halt) = 0;
    sel_external_call * (1 - sel_external_call) = 0;

    // Might be removed if derived from opcode based on a lookup of constants
    sel_mov * ( 1 - sel_mov) = 0;
    sel_cmov * ( 1 - sel_cmov) = 0;

    op_err * (1 - op_err) = 0;
    tag_err * (1 - tag_err) = 0; // Potential optimization (boolean constraint derivation from equivalence check to avm_mem)?
    id_zero * (1 - id_zero) = 0;

    // Might be removed if derived from opcode based on a lookup of constants
    mem_op_a * (1 - mem_op_a) = 0;
    mem_op_b * (1 - mem_op_b) = 0;
    mem_op_c * (1 - mem_op_c) = 0;
    mem_op_d * (1 - mem_op_d) = 0;

    rwa * (1 - rwa) = 0;
    rwb * (1 - rwb) = 0;
    rwc * (1 - rwc) = 0;
    rwd * (1 - rwd) = 0;

    // Might be removed if derived from opcode based on a lookup of constants
    ind_op_a * (1 - ind_op_a) = 0;
    ind_op_b * (1 - ind_op_b) = 0;
    ind_op_c * (1 - ind_op_c) = 0;
    ind_op_d * (1 - ind_op_d) = 0;

    // TODO - Potential constraints to be implemented:
    // - mem_idx_a, mem_idx_b, mem_idx_c, mem_idx_d to u32 type:
    //    - For direct memory accesses, this should be enforced by bytecode validation
    //      and instruction decomposition. Namely, in this case, only 32-bit immediate
    //      values should be written into these memory indices.
    //    - For indirect memory accesses, the memory trace constraints ensure that
    //      loaded values come from memory addresses with tag u32. This is enforced in the memory trace
    //      where each memory entry with flag ind_op_x (for x = a,b,c,d) constrains r_int_tag == 3 (u32).
    //
    // - ind_a, ind_b, ind_c, ind_d to u32 type: Should be guaranteed by bytecode validation and
    // instruction decomposition as only immediate 32-bit values should be written into the indirect registers.
    //
    // - 0 <= r_in_tag, w_in_tag <= 6 // This should be constrained by the operation decomposition.

    //====== COMPARATOR OPCODES CONSTRAINTS =====================================
    // Enforce that the tag for the ouput of EQ opcode is u8 (i.e. equal to 1).
    #[OUTPUT_U8]
    (sel_op_eq + sel_op_lte + sel_op_lt) * (w_in_tag - 1) = 0;

    //====== FDIV OPCODE CONSTRAINTS ============================================
    // Relation for division over the finite field
    // If tag_err == 1 in a division, then ib == 0 and op_err == 1.
    #[SUBOP_FDIV]
    sel_op_fdiv * (1 - op_err) * (ic * ib - ia) = 0;

    // When sel_op_fdiv == 1 or sel_op_div, we want ib == 0 <==> op_err == 1
    // This can be achieved with the 2 following relations.
    // inv is an extra witness to show that we can invert ib, i.e., inv = ib^(-1)
    // If ib == 0, we have to set inv = 1 to satisfy the second relation,
    // because op_err == 1 from the first relation.
    // TODO: Update the name of these relations once negative tests are updated
    #[SUBOP_FDIV_ZERO_ERR1]
    (sel_op_fdiv + sel_op_div) * (ib * inv - 1 + op_err) = 0;
    #[SUBOP_FDIV_ZERO_ERR2]
    (sel_op_fdiv + sel_op_div) * op_err * (1 - inv) = 0;

    // Enforcement that instruction tags are FF (tag constant 6).
    // TODO: These 2 conditions might be removed and enforced through
    //       the bytecode decomposition instead.
    #[SUBOP_FDIV_R_IN_TAG_FF]
    sel_op_fdiv * (r_in_tag - 6) = 0;
    #[SUBOP_FDIV_W_IN_TAG_FF]
    sel_op_fdiv * (w_in_tag - 6) = 0;

    // op_err cannot be maliciously activated for a non-relevant
    // operation selector, i.e., op_err == 1 ==> sel_op_fdiv || sel_op_XXX || ...
    // op_err * (sel_op_fdiv + sel_op_XXX + ... - 1) == 0
    // Note that the above is even a stronger constraint, as it shows
    // that exactly one sel_op_XXX must be true.
    // At this time, we have only division producing an error.
    #[SUBOP_ERROR_RELEVANT_OP]
    op_err * ((sel_op_fdiv + sel_op_div) - 1) = 0;

    // TODO: constraint that we stop execution at the first error (tag_err or op_err)
    // An error can only happen at the last sub-operation row.

    // OPEN/POTENTIAL OPTIMIZATION: Dedicated error per relevant operation?
    // For the finite field division, we could lower the degree from 4 to 3
    // (sel_op_fdiv - op_fdiv_err) * (ic * ib - ia) = 0;
    // Same for the relations related to the error activation:
    // (ib * inv - 1 + op_fdiv_err) = 0 && op_err * (1 - inv) = 0 
    // This works in combination with op_fdiv_err * (sel_op_fdiv - 1) = 0;
    // Drawback is the need to paralllelize the latter.
    
    //===== KERNEL LOOKUPS =======================================================
    pol KERNEL_INPUT_SELECTORS = (
        sel_op_sender + sel_op_address + sel_op_storage_address + sel_op_chain_id + sel_op_version + sel_op_block_number + sel_op_coinbase + 
        sel_op_timestamp + sel_op_fee_per_l2_gas + sel_op_fee_per_da_gas + sel_op_transaction_fee
    );
    // Ensure that only one kernel lookup is active when the kernel_in_offset is active
    #[KERNEL_INPUT_ACTIVE_CHECK]
    KERNEL_INPUT_SELECTORS * (1 - q_kernel_lookup) = 0;

    pol KERNEL_OUTPUT_SELECTORS = (
        sel_op_note_hash_exists + sel_op_emit_note_hash + sel_op_nullifier_exists + sel_op_emit_nullifier + sel_op_l1_to_l2_msg_exists +
        sel_op_emit_unencrypted_log + sel_op_emit_l2_to_l1_msg
    );
    #[KERNEL_OUTPUT_ACTIVE_CHECK]
    KERNEL_OUTPUT_SELECTORS * (1 - q_kernel_output_lookup) = 0;

    //===== CONTROL FLOW =======================================================
    //===== JUMP ===============================================================
    #[PC_JUMP]
    sel_jump * (pc' - ia) = 0;

    #[PC_JUMPI]
    sel_jumpi * ((1 - id_zero) * (pc' - ia) + id_zero * (pc' - pc - 1)) = 0;

    // TODO: Consolidation with #[PC_JUMP] and sel_internal_call * (pc' - ia) = 0; sel_internal_return * (pc' - ia) = 0;

    //===== INTERNAL_CALL ======================================================
    // - The program counter in the next row should be equal to the value loaded from the ia register
    // - We then write the return location (pc + 1) into the call stack (in memory)
    
    #[RETURN_POINTER_INCREMENT]
    sel_internal_call * (internal_return_ptr' - (internal_return_ptr + 1)) = 0;
    sel_internal_call * (internal_return_ptr - mem_idx_b) = 0;
    sel_internal_call * (pc' - ia) = 0;
    sel_internal_call * ((pc + 1) - ib) = 0;

    // TODO(md): Below relations may be removed through sub-op table lookup
    sel_internal_call * (rwb - 1) = 0;
    sel_internal_call * (mem_op_b  - 1) = 0;
    
    //===== INTERNAL_RETURN ===================================================
    // - We load the memory pointer to be the internal_return_ptr 
    // - Constrain then next program counter to be the loaded value
    // - decrement the internal_return_ptr

    #[RETURN_POINTER_DECREMENT]
    sel_internal_return * (internal_return_ptr' - (internal_return_ptr - 1)) = 0;
    sel_internal_return * ((internal_return_ptr - 1) - mem_idx_a) = 0;
    sel_internal_return * (pc' - ia) = 0;

    // TODO(md): Below relations may be removed through sub-op table lookup
    sel_internal_return * rwa = 0;
    sel_internal_return * (mem_op_a - 1) = 0;

    //===== CONTROL_FLOW_CONSISTENCY ============================================
    pol INTERNAL_CALL_STACK_SELECTORS = (first + sel_internal_call + sel_internal_return + sel_halt);
    pol ALL_CTRL_FLOW_SEL = sel_jump + sel_jumpi + sel_internal_call + sel_internal_return;

    pol ALL_LEFTGAS_SEL = sel_op_dagasleft + sel_op_l2gasleft;
    pol ALL_BINARY_SEL = sel_op_and + sel_op_or + sel_op_xor;
    pol ALL_GADGET_SEL = sel_op_radix_le + sel_op_sha256 + sel_op_poseidon2 + sel_op_keccak + sel_op_pedersen;
    pol ALL_MEMORY_SEL = sel_cmov + sel_mov;
    pol OPCODE_SELECTORS = sel_op_fdiv + ALU_ALL_SEL + ALL_BINARY_SEL + ALL_MEMORY_SEL + ALL_GADGET_SEL
                         + KERNEL_INPUT_SELECTORS + KERNEL_OUTPUT_SELECTORS + ALL_LEFTGAS_SEL;

    // TODO: gas_cost_active is activating gas accounting on a given row. All opcode with selectors
    //       are activated through the relation below. The other opcodes which are implemented purely
    //       through memory sub-operations such as CALLDATACOPY, RETURN, SET are activated by
    //       setting a newly introduced boolean mem_op_activate_gas which is set in witness generation.
    //       We should remove this shortcut and constrain this activation through bytecode decomposition.
    //       Alternatively, we introduce a boolean selector for the three opcodes mentioned above.
    //       Note: External call gas cost is not constrained
    pol commit gas_cost_active;
    pol commit mem_op_activate_gas; // TODO: remove this one
    // TODO: remove sload and sstore from here
    // This temporarily disables gas tracking for sload and sstore because our gas
    // tracking doesn't work properly for instructions that span multiple rows
    gas_cost_active - OPCODE_SELECTORS - ALL_CTRL_FLOW_SEL - sel_op_sload - sel_op_sstore - mem_op_activate_gas = 0;

    // Program counter must increment if not jumping or returning
    #[PC_INCREMENT]
    (1 - first) * (1 - sel_halt) * OPCODE_SELECTORS * (pc' - (pc + 1)) = 0;

    // first == 0 && sel_internal_call == 0 && sel_internal_return == 0 && sel_halt == 0 ==> internal_return_ptr == internal_return_ptr'
    #[INTERNAL_RETURN_POINTER_CONSISTENCY]
    (1 - INTERNAL_CALL_STACK_SELECTORS) * (internal_return_ptr' - internal_return_ptr) = 0; 

    // TODO: we want to set an initial number for the reserved memory of the jump pointer

    //====== SPACE ID CONSTRAINTS ===============================================
    #[SPACE_ID_INTERNAL]
    (sel_internal_call + sel_internal_return) * (space_id - constants.INTERNAL_CALL_SPACE_ID) = 0;

    #[SPACE_ID_STANDARD_OPCODES]
    OPCODE_SELECTORS * (call_ptr - space_id) = 0;

    //====== MEMORY OPCODES CONSTRAINTS =========================================

    // TODO: consolidate with zero division error handling
    // TODO: Ensure that operation decompostion will ensure mutual exclusivity of sel_cmov and sel_jumpi

    // When sel_cmov or sel_jumpi == 1, we need id == 0 <==> id_zero == 0
    // This can be achieved with the 2 following relations.
    // inv is an extra witness to show that we can invert id, i.e., inv = id^(-1)
    // If id == 0, we have to set inv = 1 to satisfy the second relation,
    // because id_zero == 1 from the first relation.
    #[CMOV_CONDITION_RES_1]
    (sel_cmov + sel_jumpi) * (id * inv - 1 + id_zero) = 0;
    #[CMOV_CONDITION_RES_2]
    (sel_cmov + sel_jumpi) * id_zero * (1 - inv) = 0;

    // Boolean selectors telling whether we move ia to ic or ib to ic.
    // Boolean constraints and mutual exclusivity are derived from their
    // respective definitions based on sel_mov, sel_cmov, and id_zero.
    pol commit sel_mov_a;
    pol commit sel_mov_b;

    // For MOV, we copy ia to ic.
    // For CMOV, we copy ia to ic if id is NOT zero, otherwise we copy ib to ic.
    sel_mov_a = sel_mov + sel_cmov * (1 - id_zero);
    sel_mov_b = sel_cmov * id_zero;

    #[MOV_SAME_VALUE_A]
    sel_mov_a * (ia - ic) = 0; // Ensure that the correct value is moved/copied.
    #[MOV_SAME_VALUE_B]
    sel_mov_b * (ib - ic) = 0; // Ensure that the correct value is moved/copied.
    #[MOV_MAIN_SAME_TAG]
    (sel_mov + sel_cmov) * (r_in_tag - w_in_tag) = 0;

    //===== ALU CONSTRAINTS =====================================================
    pol ALU_R_TAG_SEL = sel_op_add + sel_op_sub + sel_op_mul + sel_op_div + sel_op_not + sel_op_eq
                      + sel_op_lt + sel_op_lte + sel_op_shr + sel_op_shl;
    pol ALU_W_TAG_SEL = sel_op_cast;
    pol ALU_ALL_SEL = ALU_R_TAG_SEL + ALU_W_TAG_SEL;

    // Predicate to activate the copy of intermediate registers to ALU table. If tag_err == 1,
    // the operation is not copied to the ALU table.
    alu_sel = ALU_ALL_SEL * (1 - tag_err) * (1 - op_err);

    // Dispatch the correct in_tag for alu
    ALU_R_TAG_SEL * (alu_in_tag - r_in_tag) = 0;
    ALU_W_TAG_SEL * (alu_in_tag - w_in_tag) = 0;

    //===== GASLEFT OPCODES =====================================================
    #[L2GASLEFT]
    sel_op_l2gasleft * (ia - l2_gas_remaining') = 0;
    #[DAGASLEFT]
    sel_op_dagasleft * (ia - da_gas_remaining') = 0;

    //===== KERNEL INPUTS CONSTRAINTS ===========================================
    // The general pattern for environment lookups is as follows:
    //  Each kernel opcode related to some fixed positions in the `public kernel_inputs` polynomial
    //  We can lookup into a fixed index of this polynomial by including constraints that force the value
    //  of kernel_in_offset to the value relevant to the given opcode that is active

    // CALL CONTEXT
    #[SENDER_KERNEL]
    sel_op_sender * (avm_kernel.kernel_in_offset - constants.SENDER_SELECTOR) = 0;

    #[ADDRESS_KERNEL]
    sel_op_address * (avm_kernel.kernel_in_offset - constants.ADDRESS_SELECTOR) = 0;

    #[STORAGE_ADDRESS_KERNEL]
    sel_op_storage_address * (avm_kernel.kernel_in_offset - constants.STORAGE_ADDRESS_SELECTOR) = 0;

    // FEES
    #[FEE_DA_GAS_KERNEL]
    sel_op_fee_per_da_gas  * (avm_kernel.kernel_in_offset - constants.FEE_PER_DA_GAS_SELECTOR) = 0;

    #[FEE_L2_GAS_KERNEL]
    sel_op_fee_per_l2_gas  * (avm_kernel.kernel_in_offset - constants.FEE_PER_L2_GAS_SELECTOR) = 0;

    #[FEE_TRANSACTION_FEE_KERNEL]
    sel_op_transaction_fee  * (avm_kernel.kernel_in_offset - constants.TRANSACTION_FEE_SELECTOR) = 0;

    // GLOBALS
    #[CHAIN_ID_KERNEL]
    sel_op_chain_id * (avm_kernel.kernel_in_offset - constants.CHAIN_ID_SELECTOR) = 0;

    #[VERSION_KERNEL]
    sel_op_version * (avm_kernel.kernel_in_offset - constants.VERSION_SELECTOR) = 0;

    #[BLOCK_NUMBER_KERNEL]
    sel_op_block_number * (avm_kernel.kernel_in_offset - constants.BLOCK_NUMBER_SELECTOR) = 0;

    #[COINBASE_KERNEL]
    sel_op_coinbase * (avm_kernel.kernel_in_offset - constants.COINBASE_SELECTOR) = 0;

    #[TIMESTAMP_KERNEL]
    sel_op_timestamp * (avm_kernel.kernel_in_offset - constants.TIMESTAMP_SELECTOR) = 0;

    // OUTPUTS LOOKUPS
    // Constrain the value of kernel_out_offset to be the correct offset for the operation being performed
    #[NOTE_HASH_KERNEL_OUTPUT]
    sel_op_note_hash_exists * (avm_kernel.kernel_out_offset - (avm_kernel.START_NOTE_HASH_EXISTS_WRITE_OFFSET + avm_kernel.note_hash_exist_write_offset)) = 0;
    first * avm_kernel.note_hash_exist_write_offset = 0;
    

    #[EMIT_NOTE_HASH_KERNEL_OUTPUT]
    sel_op_emit_note_hash * (avm_kernel.kernel_out_offset - (avm_kernel.START_EMIT_NOTE_HASH_WRITE_OFFSET + avm_kernel.emit_note_hash_write_offset)) = 0;
    first * avm_kernel.emit_note_hash_write_offset = 0;

    #[NULLIFIER_EXISTS_KERNEL_OUTPUT]
    sel_op_nullifier_exists * (avm_kernel.kernel_out_offset - ((ib * (avm_kernel.START_NULLIFIER_EXISTS_OFFSET + avm_kernel.nullifier_exists_write_offset)) + ((1 - ib) * (avm_kernel.START_NULLIFIER_NON_EXISTS_OFFSET + avm_kernel.nullifier_non_exists_write_offset)))) = 0;
    first * avm_kernel.nullifier_exists_write_offset = 0;
    first * avm_kernel.nullifier_non_exists_write_offset = 0;

    #[EMIT_NULLIFIER_KERNEL_OUTPUT]
    sel_op_emit_nullifier * (avm_kernel.kernel_out_offset - (avm_kernel.START_EMIT_NULLIFIER_WRITE_OFFSET + avm_kernel.emit_nullifier_write_offset)) = 0;
    first * avm_kernel.emit_nullifier_write_offset = 0;

    #[L1_TO_L2_MSG_EXISTS_KERNEL_OUTPUT]
    sel_op_l1_to_l2_msg_exists * (avm_kernel.kernel_out_offset - (avm_kernel.START_L1_TO_L2_MSG_EXISTS_WRITE_OFFSET + avm_kernel.l1_to_l2_msg_exists_write_offset)) = 0;
    first * avm_kernel.l1_to_l2_msg_exists_write_offset = 0;

    #[EMIT_UNENCRYPTED_LOG_KERNEL_OUTPUT]
    sel_op_emit_unencrypted_log * (avm_kernel.kernel_out_offset - (avm_kernel.START_EMIT_UNENCRYPTED_LOG_WRITE_OFFSET + avm_kernel.emit_unencrypted_log_write_offset)) = 0;
    first * avm_kernel.emit_unencrypted_log_write_offset = 0;

    // TODO: Add the equivalent for GETCONTRACTINSTANCE?

    #[EMIT_L2_TO_L1_MSGS_KERNEL_OUTPUT]
    sel_op_emit_l2_to_l1_msg * (avm_kernel.kernel_out_offset - (avm_kernel.START_EMIT_L2_TO_l1_MSG + avm_kernel.emit_l2_to_l1_msg_write_offset)) = 0;
    first * avm_kernel.emit_l2_to_l1_msg_write_offset = 0;

    #[SLOAD_KERNEL_OUTPUT]
    sel_op_sload * (avm_kernel.kernel_out_offset - (avm_kernel.START_SLOAD_WRITE_OFFSET + avm_kernel.sload_write_offset)) = 0;
    first * avm_kernel.sload_write_offset = 0;

    #[SSTORE_KERNEL_OUTPUT]
    sel_op_sstore * (avm_kernel.kernel_out_offset - (avm_kernel.START_SSTORE_WRITE_OFFSET + avm_kernel.sstore_write_offset)) = 0;
    first * avm_kernel.sstore_write_offset = 0;

    // When we encounter a state writing opcode
    // We increment the side effect counter by 1
    KERNEL_OUTPUT_SELECTORS * (avm_kernel.side_effect_counter' - (avm_kernel.side_effect_counter + 1)) = 0;

    #[KERNEL_OUTPUT_LOOKUP]
    q_kernel_output_lookup {avm_kernel.kernel_out_offset, ia, avm_kernel.side_effect_counter, ib} in avm_kernel.q_public_input_kernel_out_add_to_table {clk, avm_kernel.kernel_value_out, avm_kernel.kernel_side_effect_out, avm_kernel.kernel_metadata_out};

    #[LOOKUP_INTO_KERNEL]
    q_kernel_lookup { avm_main.ia, avm_kernel.kernel_in_offset } in avm_kernel.q_public_input_kernel_add_to_table { avm_kernel.kernel_inputs, clk };

    //====== Inter-table Constraints ============================================
    #[INCL_MAIN_TAG_ERR]
    avm_mem.tag_err {avm_mem.clk} in tag_err {clk};

    #[INCL_MEM_TAG_ERR]
    tag_err {clk} in avm_mem.tag_err {avm_mem.clk};

    #[PERM_MAIN_ALU]
    alu_sel {clk, ia, ib, ic, sel_op_add, sel_op_sub,
             sel_op_mul, sel_op_div, sel_op_eq, sel_op_not, sel_op_cast,
             sel_op_lt, sel_op_lte, sel_op_shr, sel_op_shl, alu_in_tag}
    is
    avm_alu.alu_sel {avm_alu.clk, avm_alu.ia, avm_alu.ib, avm_alu.ic, avm_alu.op_add, avm_alu.op_sub,
                     avm_alu.op_mul, avm_alu.op_div, avm_alu.op_eq, avm_alu.op_not, avm_alu.op_cast,
                     avm_alu.op_lt, avm_alu.op_lte, avm_alu.op_shr, avm_alu.op_shl, avm_alu.in_tag};

    // Based on the boolean selectors, we derive the binary op id to lookup in the table;
    // TODO: Check if having 4 columns (op_id + 3 boolean selectors) is more optimal that just using the op_id
    // but with a higher degree constraint: op_id * (op_id - 1) * (op_id - 2)
    pol commit bin_op_id;
    #[BIN_SEL_1]
    bin_op_id = sel_op_or + 2 * sel_op_xor; // sel_op_and excluded since op_id = 0 for op_and

    // Only 1 of the binary selectors should be set (i.e. Mutual Exclusivity)
    // Bin_sel is not explicitly constrained to be boolean, however this is enforced through
    // the operation decomposition step during bytecode unpacking.
    #[BIN_SEL_2]
    bin_sel = sel_op_and + sel_op_or + sel_op_xor;

    #[PERM_MAIN_BIN]
    bin_sel {clk, ia, ib, ic, bin_op_id, r_in_tag}
    is
    avm_binary.start {avm_binary.clk, avm_binary.acc_ia, avm_binary.acc_ib, avm_binary.acc_ic, avm_binary.op_id, avm_binary.in_tag};

    #[PERM_MAIN_CONV]
    sel_op_radix_le {clk, ia, ic, id}
    is
    avm_conversion.to_radix_le_sel {avm_conversion.clk, avm_conversion.input, avm_conversion.radix, avm_conversion.num_limbs};

    // This will be enabled when we migrate just to sha256Compression, as getting sha256 to work with it is tricky.
    // #[PERM_MAIN_SHA256]
    // sel_op_sha256 {clk, ia, ib, ic}
    // is
    // avm_sha256.sha256_compression_sel {avm_sha256.clk, avm_sha256.state, avm_sha256.input, avm_sha256.output};

    #[PERM_MAIN_POS2_PERM]
    sel_op_poseidon2 {clk, ia, ib}
    is
    avm_poseidon2.poseidon_perm_sel {avm_poseidon2.clk, avm_poseidon2.input, avm_poseidon2.output};

    // This will be enabled when we migrate just to keccakf1600, as getting keccak to work with it is tricky.
    // #[PERM_MAIN_KECCAK]
    // sel_op_keccak {clk, ia, ic}
    // is
    // avm_keccakf1600.keccakf1600_sel {avm_keccakf1600.clk, avm_keccakf1600.input, avm_keccakf1600.output};

    #[PERM_MAIN_PEDERSEN]
    sel_op_pedersen {clk, ia}
    is
    avm_pedersen.pedersen_sel {avm_pedersen.clk, avm_pedersen.input};

    #[PERM_MAIN_MEM_A]
    mem_op_a {clk, space_id, mem_idx_a, ia, rwa
            , r_in_tag, w_in_tag, sel_mov_a, sel_cmov}
    is
    avm_mem.op_a {avm_mem.clk, avm_mem.space_id, avm_mem.addr, avm_mem.val, avm_mem.rw
                , avm_mem.r_in_tag, avm_mem.w_in_tag, avm_mem.sel_mov_a, avm_mem.sel_cmov};

    #[PERM_MAIN_MEM_B]
    mem_op_b {clk, space_id, mem_idx_b, ib, rwb
            , r_in_tag, w_in_tag, sel_mov_b, sel_cmov}
    is
    avm_mem.op_b {avm_mem.clk, avm_mem.space_id, avm_mem.addr, avm_mem.val, avm_mem.rw
                , avm_mem.r_in_tag, avm_mem.w_in_tag, avm_mem.sel_mov_b, avm_mem.sel_cmov};

    #[PERM_MAIN_MEM_C]
    mem_op_c {clk, space_id, mem_idx_c, ic, rwc
            , r_in_tag, w_in_tag}
    is
    avm_mem.op_c {avm_mem.clk, avm_mem.space_id, avm_mem.addr, avm_mem.val, avm_mem.rw
                , avm_mem.r_in_tag, avm_mem.w_in_tag};

    #[PERM_MAIN_MEM_D]
    mem_op_d {clk, space_id, mem_idx_d, id, rwd
            , r_in_tag, w_in_tag, sel_cmov}
    is
    avm_mem.op_d {avm_mem.clk, avm_mem.space_id, avm_mem.addr, avm_mem.val, avm_mem.rw
                , avm_mem.r_in_tag, avm_mem.w_in_tag, avm_mem.sel_cmov};

    #[PERM_MAIN_MEM_IND_A]
    ind_op_a {clk, space_id, ind_a, mem_idx_a}
    is
    avm_mem.ind_op_a {avm_mem.clk, avm_mem.space_id, avm_mem.addr, avm_mem.val};

    #[PERM_MAIN_MEM_IND_B]
    ind_op_b {clk, space_id, ind_b, mem_idx_b}
    is
    avm_mem.ind_op_b {avm_mem.clk, avm_mem.space_id, avm_mem.addr, avm_mem.val};

    #[PERM_MAIN_MEM_IND_C]
    ind_op_c {clk, space_id, ind_c, mem_idx_c}
    is
    avm_mem.ind_op_c {avm_mem.clk, avm_mem.space_id, avm_mem.addr, avm_mem.val};

    #[PERM_MAIN_MEM_IND_D]
    ind_op_d {clk, space_id, ind_d, mem_idx_d}
    is
    avm_mem.ind_op_d {avm_mem.clk, avm_mem.space_id, avm_mem.addr, avm_mem.val};

    #[LOOKUP_MEM_RNG_CHK_LO]
    avm_mem.rng_chk_sel {avm_mem.diff_lo} in sel_rng_16 {clk};

    #[LOOKUP_MEM_RNG_CHK_MID] 
    avm_mem.rng_chk_sel {avm_mem.diff_mid} in sel_rng_16 {clk};

    #[LOOKUP_MEM_RNG_CHK_HI]
    avm_mem.rng_chk_sel {avm_mem.diff_hi} in sel_rng_8 {clk};

    //====== Inter-table Shift Constraints (Lookups) ============================================
    // Currently only used for shift operations but can be generalised for other uses.

    // Lookup for 2**(ib)
    #[LOOKUP_POW_2_0]
    avm_alu.shift_sel {avm_alu.ib, avm_alu.two_pow_s} in sel_rng_8 {clk, table_pow_2};

    // Lookup for 2**(t-ib)
    #[LOOKUP_POW_2_1]
    avm_alu.shift_sel {avm_alu.t_sub_s_bits , avm_alu.two_pow_t_sub_s} in sel_rng_8 {clk, table_pow_2};

    //====== Inter-table Constraints (Range Checks) ============================================
    // TODO: Investigate optimising these range checks. Handling non-FF elements should require less range checks.
    //       One can increase the granularity based on the operation and tag. In the most extreme case,
    //       a specific selector per register might be introduced.
    #[LOOKUP_U8_0]
    avm_alu.rng_chk_lookup_selector { avm_alu.u8_r0 } in sel_rng_8 { clk };

    #[LOOKUP_U8_1]
    avm_alu.rng_chk_lookup_selector { avm_alu.u8_r1 } in sel_rng_8 { clk };

    #[LOOKUP_U16_0]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r0 } in sel_rng_16 { clk };

    #[LOOKUP_U16_1]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r1 } in sel_rng_16 { clk };

    #[LOOKUP_U16_2]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r2 } in sel_rng_16 { clk };

    #[LOOKUP_U16_3]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r3 } in sel_rng_16 { clk };

    #[LOOKUP_U16_4]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r4 } in sel_rng_16 { clk };

    #[LOOKUP_U16_5]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r5 } in sel_rng_16 { clk };

    #[LOOKUP_U16_6]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r6 } in sel_rng_16 { clk };

    #[LOOKUP_U16_7]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r7 } in sel_rng_16 { clk };

    #[LOOKUP_U16_8]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r8 } in sel_rng_16 { clk };

    #[LOOKUP_U16_9]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r9 } in sel_rng_16 { clk };

    #[LOOKUP_U16_10]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r10 } in sel_rng_16 { clk };

    #[LOOKUP_U16_11]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r11 } in sel_rng_16 { clk };

    #[LOOKUP_U16_12]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r12 } in sel_rng_16 { clk };

    #[LOOKUP_U16_13]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r13 } in sel_rng_16 { clk };

    #[LOOKUP_U16_14]
    avm_alu.rng_chk_lookup_selector {avm_alu.u16_r14 } in sel_rng_16 { clk };

    // ==== Additional row range checks for division
    #[LOOKUP_DIV_U16_0]
    avm_alu.div_rng_chk_selector {avm_alu.div_u16_r0} in sel_rng_16 { clk };

    #[LOOKUP_DIV_U16_1]
    avm_alu.div_rng_chk_selector {avm_alu.div_u16_r1 } in sel_rng_16 { clk };

    #[LOOKUP_DIV_U16_2]
    avm_alu.div_rng_chk_selector {avm_alu.div_u16_r2 } in sel_rng_16 { clk };

    #[LOOKUP_DIV_U16_3]
    avm_alu.div_rng_chk_selector {avm_alu.div_u16_r3 } in sel_rng_16 { clk };

    #[LOOKUP_DIV_U16_4]
    avm_alu.div_rng_chk_selector {avm_alu.div_u16_r4 } in sel_rng_16 { clk };

    #[LOOKUP_DIV_U16_5]
    avm_alu.div_rng_chk_selector {avm_alu.div_u16_r5 } in sel_rng_16 { clk };

    #[LOOKUP_DIV_U16_6]
    avm_alu.div_rng_chk_selector {avm_alu.div_u16_r6 } in sel_rng_16 { clk };

    #[LOOKUP_DIV_U16_7]
    avm_alu.div_rng_chk_selector {avm_alu.div_u16_r7 } in sel_rng_16 { clk };
