include "avm_main.pil";

namespace avm_alu(256);

    // ========= Table ALU-TR =================================================

    // References to main trace table of sub-operations, clk, intermediate
    // registers, operation selectors.
    // TODO: Think on optimizations to decrease the number of such "copied" columns
    pol commit alu_clk;
    pol commit alu_ia; // Intermediate registers
    pol commit alu_ib;
    pol commit alu_ic;
    pol commit alu_op_add; // Operation selectors
    pol commit alu_op_sub;
    pol commit alu_op_mul;
    pol commit alu_op_div;
    pol commit alu_op_not;
    pol commit alu_op_eq;
    pol commit alu_sel; // Predicate to activate the copy of intermediate registers to ALU table.

    // Instruction tag (1: u8, 2: u16, 3: u32, 4: u64, 5: u128, 6: field) copied from Main table
    pol commit alu_in_tag;

    // Flattened boolean instruction tags
    pol commit alu_ff_tag;
    pol commit alu_u8_tag;
    pol commit alu_u16_tag;
    pol commit alu_u32_tag;
    pol commit alu_u64_tag;
    pol commit alu_u128_tag;

    // 8-bit slice registers
    pol commit alu_u8_r0;
    pol commit alu_u8_r1;

    // 16-bit slice registers
    pol commit alu_u16_r0;
    pol commit alu_u16_r1;
    pol commit alu_u16_r2;
    pol commit alu_u16_r3;
    pol commit alu_u16_r4;
    pol commit alu_u16_r5;
    pol commit alu_u16_r6;
    pol commit alu_u16_r7;

    // 64-bit slice register
    pol commit alu_u64_r0;

    // Carry flag
    pol commit alu_cf;

    // Compute predicate telling whether there is a row entry in the ALU table.
    alu_sel = alu_op_add + alu_op_sub + alu_op_mul + alu_op_not + alu_op_eq;

    // ========= Type Constraints =============================================
    // TODO: Range constraints
    //       - for slice registers
    //       - intermediate registers ia and ib (inputs) depending on flag (or inherited from previous ops?)
    //       - intermediate register ic (in some operations it might be inherited based on ia and ib ranges. To be checked.)
    //       Carry flag: We will have to constraint to ensure that the 
    //                   arithmetic expressions are not overflowing finite field size
    // Remark: Operation selectors are constrained in the main trace.

    // Boolean flattened instructions tags
    alu_ff_tag * (1 - alu_ff_tag) = 0;
    alu_u8_tag * (1 - alu_u8_tag) = 0;
    alu_u16_tag * (1 - alu_u16_tag) = 0;
    alu_u32_tag * (1 - alu_u32_tag) = 0;
    alu_u64_tag * (1 - alu_u64_tag) = 0;
    alu_u128_tag * (1 - alu_u128_tag) = 0;

    // Mutual exclusion of the flattened instruction tag.
    alu_sel * (alu_ff_tag + alu_u8_tag + alu_u16_tag + alu_u32_tag + alu_u64_tag + alu_u128_tag - 1) = 0;

    // Correct flattening of the instruction tag.
    alu_in_tag = alu_u8_tag + 2 * alu_u16_tag + 3 * alu_u32_tag + 4 * alu_u64_tag + 5 * alu_u128_tag + 6 * alu_ff_tag;

    // Operation selectors are copied from main table and do not need to be constrained here.
    // TODO: Ensure mutual exclusion of alu_op_add and alu_op_sub as some relations below
    //       requires it.

    // ========= ARITHMETIC OPERATION - EXPLANATIONS =================================================
    // Main trick for arithmetic operations modulo 2^k is to perform the operation
    // over the integers and expressing the result as low + high * 2^k with low
    // smaller than 2^k. low is used as the output. This works as long this does
    // not underflow/overflow the underlying finite field order (u128 multiplication
    // will be handled differently). If we want to prove that c = OP(a,b) where OP
    // denotes an arithmetic operation modulo 2^k, we can achieve this with two relations:
    // (1) low + high * 2^k - OP'(a,b) = 0
    // (2) low - c = 0
    //
    // where OP' denotes the same operation as OP but over the integers (not mod 2^k).
    // We support u8, u16, u32, u64, u128 types and decompose low into
    // smaller bit slices, e.g., 16. For instance, low = s_0 + s_1 * 2^16 + s_2 * 2^32 + ...
    // The slices have to be range constrained and there is a trade-off between the
    // number of registers and complexity of the range constraints.
    //
    // Optimization: We will capture the relation (1) for all types under the same umbrella
    //               by re-using the decomposition used for u128 type for the lower types.
    //               This works because any larger power of 2^k divides 2^l whenever k <= l.
    //               To be able to express "low" for u8, we need a 8-bit limb for the lowest
    //               bits. A bit decomposition covering all types is:
    //  s8_0 + s8_1 * 2^8 + s16_0 * 2^16 + s16_1 * 2^32 ... + s16_6 * 2^112 + carry * 2^128 - OP'(a,b) = 0
    //               where s8_i's are 8-bit registers and s16's 16-bit registers.
    //               For type uk, we set low to the k-bit truncated part of register decomposition.
    //               As example, for u32: low = s8_0 + s8_1 * 2^8 + s16_0 * 2^16
    //               and for u8: low = s8_0
    //
    // TODO: It is open whether we might get efficiency gain to use larger registers for the higher
    //       parts of the bit decomposition.

    // ============= Helper polynomial terms ============================
    // These are intermediate polynomial terms which are not commited but
    // serves to an algebraic expression of commited polynomials in a more concise way.

    // Bit slices partial sums
    pol SUM_8   = alu_u8_r0;
    pol SUM_16  = SUM_8      + alu_u8_r1 * 2**8;
    pol SUM_32  = SUM_16     + alu_u16_r0 * 2**16;
    pol SUM_64  = SUM_32     + alu_u16_r1 * 2**32 + alu_u16_r2 * 2**48;
    pol SUM_96  = SUM_64     + alu_u16_r3 * 2**64 + alu_u16_r4 * 2**80;
    pol SUM_128 = SUM_96     + alu_u16_r5 * 2**96 + alu_u16_r6 * 2**112;

    // ========= ADDITION/SUBTRACTION Operation Constraints ===============================
    //
    // Addition and subtraction relations are very similar and will be consolidated.
    // For the addition we have to replace OP'(a,b) in the above relation by a+b and
    // for subtraction by a-b. Using operation selector values to toggle "+b" vs. "-b"
    // makes the deal with the adaptation that the carry term needs to be subtracted
    // instead of being added. To sumarize, for the first relation, addition needs to
    // satisfy:
    // sum_128 + carry * 2^128 - a - b = 0
    // while the subtraction satisfies:
    // sum_128 - carry * 2^128 - a + b = 0
    //
    // Finally, we would like this relation to also satisfy the addition over the finite field.
    // For this, we add c in the relation conditoned by the ff type selector alu_ff_tag. We emphasize
    // that this relation alone for FF will not imply correctness of the FF addition. We only want
    // it to be satisfied. A separate relation will ensure correctness of it.
    //
    // The second relation will consist in showing that sum_N - c = 0 for N = 8, 16, 32, 64, 128.

    #[ALU_ADD_SUB_1]
    (alu_op_add + alu_op_sub) * (SUM_128 - alu_ia + alu_ff_tag * alu_ic) + (alu_op_add - alu_op_sub) * (alu_cf * 2**128 - alu_ib) = 0;

    // Helper polynomial
    pol SUM_TAG = alu_u8_tag * SUM_8 + alu_u16_tag * SUM_16 + alu_u32_tag * SUM_32 + alu_u64_tag * SUM_64 + alu_u128_tag * SUM_128;

    #[ALU_ADD_SUB_2]
    (alu_op_add + alu_op_sub) * (SUM_TAG + alu_ff_tag * alu_ia - alu_ic) + alu_ff_tag * (alu_op_add - alu_op_sub) * alu_ib = 0;

    // ========= MULTIPLICATION Operation Constraints ===============================

    // ff multiplication
    #[ALU_MULTIPLICATION_FF]
    alu_ff_tag * alu_op_mul * (alu_ia * alu_ib - alu_ic) = 0;


    // We need 2k bits to express the product (a*b) over the integer, i.e., for type uk
    // we express the product as sum_k (u8 is an exception as we need 8-bit registers)

    // We group relations for u8, u16, u32, u64 together.

    // Helper polynomial
    pol SUM_TAG_NO_128 = alu_u8_tag * SUM_8 + alu_u16_tag * SUM_16 + alu_u32_tag * SUM_32 + alu_u64_tag * SUM_64;

    #[ALU_MUL_COMMON_1]
    (1 - alu_ff_tag - alu_u128_tag) * alu_op_mul * (SUM_128 - alu_ia * alu_ib) = 0;

    #[ALU_MUL_COMMON_2]
    alu_op_mul * (SUM_TAG_NO_128 - (1 - alu_ff_tag - alu_u128_tag) * alu_ic) = 0;

    // ========= u128 MULTIPLICATION Operation Constraints ===============================
    //
    // We express a, b in 64-bit slices: a = a_l + a_h * 2^64
    //                                   b = b_l + b_h * 2^64
    // We show that c satisfies: a_l * b_l + (a_h * b_l + a_l * b_h) * 2^64 = R * 2^128 + c
    // for a R < 2^65. Equivalently:
    // a * b_l + a_l * b_h * 2^64 = (CF * 2^64 + R') * 2^128 + c
    // for a bit carry flag CF and R' range constrained to 64 bits.
    // We use two lines in the execution trace. First line represents a 
    // as decomposed over 16-bit registers. Second line represents b.
    // Selector flag is only toggled in the first line and we access b through
    // shifted polynomials.
    // R' is stored in alu_u64_r0

    // 64-bit lower limb
    pol SUM_LOW_64 = alu_u16_r0 + alu_u16_r1 * 2**16 + alu_u16_r2 * 2**32 + alu_u16_r3 * 2**48;

    // 64-bit higher limb
    pol SUM_HIGH_64 = alu_u16_r4 + alu_u16_r5 * 2**16 + alu_u16_r6 * 2**32 + alu_u16_r7 * 2**48;

    // 64-bit lower limb for next row
    pol SUM_LOW_SHIFTED_64 = alu_u16_r0' + alu_u16_r1' * 2**16 + alu_u16_r2' * 2**32 + alu_u16_r3' * 2**48;

    // 64-bit higher limb for next row
    pol SUM_HIGH_SHIFTED_64 = alu_u16_r4' + alu_u16_r5' * 2**16 + alu_u16_r6' * 2**32 + alu_u16_r7' * 2**48;

    // Arithmetic relations
    alu_u128_tag * alu_op_mul * (SUM_LOW_64 + SUM_HIGH_64 * 2**64 - alu_ia) = 0;
    alu_u128_tag * alu_op_mul * (SUM_LOW_SHIFTED_64 + SUM_HIGH_SHIFTED_64 * 2**64 - alu_ib) = 0;
    #[ALU_MULTIPLICATION_OUT_U128]
    alu_u128_tag * alu_op_mul * (
            alu_ia * SUM_LOW_SHIFTED_64
          + SUM_LOW_64 * SUM_HIGH_SHIFTED_64 * 2**64
          - (alu_cf * 2**64 + alu_u64_r0) * 2**128
          - alu_ic
        ) = 0;

    // ========= BITWISE NOT Operation Constraints ===============================
    // Constrain mem_tag to not be FF (BITWISE NOT doesn't make sense for FF)
    // TODO decide if it is done here or in another trace

    // Do not allow alu_ff_tag to be set if we are doing bitwise
    pol BITWISE_SEL = alu_op_not; // Add more bitwise operations
    #[ALU_FF_NOT_XOR]
    BITWISE_SEL * alu_ff_tag = 0;

    // The value 2^k - 1
    pol UINT_MAX = alu_u8_tag * 2**8 + 
                alu_u16_tag * 2**16 +
                alu_u32_tag * 2**32 +
                alu_u64_tag * 2**64 +
                alu_u128_tag * 2**128 - 1;

    // BITWISE NOT relation is: a + ~a = 2^k - 1
    // Or (a + ~a - 2^k + 1) = 0;
    // value of "a" stored in alu_ia and "~a" stored in alu_ic
    #[ALU_OP_NOT]
    alu_op_not * (alu_ia + alu_ic - UINT_MAX) = 0;

    // ========= EQUALITY Operation Constraints ===============================
    // TODO: Note this method differs from the approach taken for "equality to zero" checks
    // in handling the error tags found in avm_main and avm_mem files. The predicted relation difference
    // is minor and when we optimise we will harmonise the methods based on actual performance.

    // Equality of two elements is found by performing an "equality to zero" check.
    // This relies on the fact that the inverse of a field element exists for all elements except zero
    // 1) Given two values x & y, find the difference z = x - y
    // 2) If x & y are equal, z == 0 otherwise z != 0
    // 3) Field equality to zero can be done as follows
    //   a) z(e(x - w) + w) - 1 + e = 0;
    //   b) where w = z^-1 and e is a boolean value indicating if z == 0
    //   c) if e == 0; zw = 1 && z has an inverse. If e == 1; z == 0 and we set w = 0;

    // Registers Ia and Ib hold the values that equality is to be tested on
    pol DIFF = alu_ia - alu_ib;

    // Need an additional helper that holds the inverse of the difference;
    pol commit alu_op_eq_diff_inv;

    // If EQ selector is set, ic needs to be boolean
    #[ALU_RES_IS_BOOL]
    alu_op_eq * (alu_ic * (1 - alu_ic)) = 0;

    #[ALU_OP_EQ]
    alu_op_eq * (DIFF * (alu_ic * (1 - alu_op_eq_diff_inv) + alu_op_eq_diff_inv) - 1 + alu_ic) = 0;


