---
title: Hash methods
description:
  Learn about the cryptographic primitives ready to use for any Noir project, including sha256,
  blake2s, pedersen, mimc_bn254 and mimc
keywords:
  [cryptographic primitives, Noir project, sha256, blake2s, pedersen, mimc_bn254, mimc, hash]
sidebar_position: 0
---

import BlackBoxInfo from '@site/src/components/Notes/_blackbox.mdx';

## sha256

Given an array of bytes, returns the resulting sha256 hash.

```rust title="sha256" showLineNumbers 
pub fn sha256<N>(input: [u8; N]) -> [u8; 32]
```
> <sup><sub><a href="https://github.com/noir-lang/noir/blob/master/noir_stdlib/src/hash.nr#L9-L11" target="_blank" rel="noopener noreferrer">Source code: noir_stdlib/src/hash.nr#L9-L11</a></sub></sup>


example:

```rust
fn main() {
    let x = [163, 117, 178, 149]; // some random bytes
    let hash = std::hash::sha256(x);
}
```

<BlackBoxInfo />

## blake2s

Given an array of bytes, returns an array with the Blake2 hash

```rust title="blake2s" showLineNumbers 
pub fn blake2s<N>(input: [u8; N]) -> [u8; 32]
```
> <sup><sub><a href="https://github.com/noir-lang/noir/blob/master/noir_stdlib/src/hash.nr#L15-L17" target="_blank" rel="noopener noreferrer">Source code: noir_stdlib/src/hash.nr#L15-L17</a></sub></sup>


example:

```rust
fn main() {
    let x = [163, 117, 178, 149]; // some random bytes
    let hash = std::hash::blake2s(x);
}
```

<BlackBoxInfo />

## blake3

Given an array of bytes, returns an array with the Blake3 hash

```rust title="blake3" showLineNumbers 
pub fn blake3<N>(input: [u8; N]) -> [u8; 32]
```
> <sup><sub><a href="https://github.com/noir-lang/noir/blob/master/noir_stdlib/src/hash.nr#L21-L23" target="_blank" rel="noopener noreferrer">Source code: noir_stdlib/src/hash.nr#L21-L23</a></sub></sup>


example:

```rust
fn main() {
    let x = [163, 117, 178, 149]; // some random bytes
    let hash = std::hash::blake3(x);
}
```

<BlackBoxInfo />

## pedersen_hash

Given an array of Fields, returns the Pedersen hash.

```rust title="pedersen_hash" showLineNumbers 
pub fn pedersen_hash<N>(input: [Field; N]) -> Field
```
> <sup><sub><a href="https://github.com/noir-lang/noir/blob/master/noir_stdlib/src/hash.nr#L46-L48" target="_blank" rel="noopener noreferrer">Source code: noir_stdlib/src/hash.nr#L46-L48</a></sub></sup>


example:

```rust title="pedersen-hash" showLineNumbers 
use dep::std;

fn main(x: Field, y: Field, expected_hash: Field) {
    let hash = std::hash::pedersen_hash([x, y]);
    assert_eq(hash, expected_hash);
}
```
> <sup><sub><a href="https://github.com/noir-lang/noir/blob/master/test_programs/execution_success/pedersen_hash/src/main.nr#L1-L8" target="_blank" rel="noopener noreferrer">Source code: test_programs/execution_success/pedersen_hash/src/main.nr#L1-L8</a></sub></sup>


<BlackBoxInfo />


## pedersen_commitment

Given an array of Fields, returns the Pedersen commitment.

```rust title="pedersen_commitment" showLineNumbers 
struct PedersenPoint {
   x : Field,
   y : Field,
}

pub fn pedersen_commitment<N>(input: [Field; N]) -> PedersenPoint
```
> <sup><sub><a href="https://github.com/noir-lang/noir/blob/master/noir_stdlib/src/hash.nr#L26-L33" target="_blank" rel="noopener noreferrer">Source code: noir_stdlib/src/hash.nr#L26-L33</a></sub></sup>


example:

```rust title="pedersen-commitment" showLineNumbers 
use dep::std;

fn main(x: Field, y: Field, expected_commitment: std::hash::PedersenPoint) {
    let commitment = std::hash::pedersen_commitment([x, y]);
    assert_eq(commitment.x, expected_commitment.x);
    assert_eq(commitment.y, expected_commitment.y);
}
```
> <sup><sub><a href="https://github.com/noir-lang/noir/blob/master/test_programs/execution_success/pedersen_commitment/src/main.nr#L1-L9" target="_blank" rel="noopener noreferrer">Source code: test_programs/execution_success/pedersen_commitment/src/main.nr#L1-L9</a></sub></sup>


<BlackBoxInfo />

## keccak256

Given an array of bytes (`u8`), returns the resulting keccak hash as an array of 32 bytes
(`[u8; 32]`). Specify a message_size to hash only the first `message_size` bytes
of the input.

```rust title="keccak256" showLineNumbers 
pub fn keccak256<N>(input: [u8; N], message_size: u32) -> [u8; 32]
```
> <sup><sub><a href="https://github.com/noir-lang/noir/blob/master/noir_stdlib/src/hash.nr#L71-L73" target="_blank" rel="noopener noreferrer">Source code: noir_stdlib/src/hash.nr#L71-L73</a></sub></sup>


example:

```rust title="keccak256" showLineNumbers 
use dep::std;

fn main(x: Field, result: [u8; 32]) {
    // We use the `as` keyword here to denote the fact that we want to take just the first byte from the x Field
    // The padding is taken care of by the program
    let digest = std::hash::keccak256([x as u8], 1);
    assert(digest == result);

    //#1399: variable message size
    let message_size = 4;
    let hash_a = std::hash::keccak256([1, 2, 3, 4], message_size);
    let hash_b = std::hash::keccak256([1, 2, 3, 4, 0, 0, 0, 0], message_size);

    assert(hash_a == hash_b);

    let message_size_big = 8;
    let hash_c = std::hash::keccak256([1, 2, 3, 4, 0, 0, 0, 0], message_size_big);

    assert(hash_a != hash_c);
}
```
> <sup><sub><a href="https://github.com/noir-lang/noir/blob/master/test_programs/execution_success/keccak256/src/main.nr#L1-L22" target="_blank" rel="noopener noreferrer">Source code: test_programs/execution_success/keccak256/src/main.nr#L1-L22</a></sub></sup>


<BlackBoxInfo />

## poseidon

Given an array of Fields, returns a new Field with the Poseidon Hash. Mind that you need to specify
how many inputs are there to your Poseidon function.

```rust
// example for hash_1, hash_2 accepts an array of length 2, etc
fn hash_1(input: [Field; 1]) -> Field
```

example:

```rust title="poseidon" showLineNumbers 
use dep::std::hash::poseidon;
use dep::std::hash::poseidon2;

fn main(x1: [Field; 2], y1: pub Field, x2: [Field; 4], y2: pub Field, x3: [Field; 4], y3: Field) {
    let hash1 = poseidon::bn254::hash_2(x1);
    assert(hash1 == y1);

    let hash2 = poseidon::bn254::hash_4(x2);
    assert(hash2 == y2);

    let hash3 = poseidon2::Poseidon2::hash(x3, x3.len());
    assert(hash3 == y3);
}
```
> <sup><sub><a href="https://github.com/noir-lang/noir/blob/master/test_programs/execution_success/poseidon_bn254_hash/src/main.nr#L1-L15" target="_blank" rel="noopener noreferrer">Source code: test_programs/execution_success/poseidon_bn254_hash/src/main.nr#L1-L15</a></sub></sup>


## poseidon 2

Given an array of Fields, returns a new Field with the Poseidon2 Hash. Contrary to the Poseidon
function, there is only one hash and you can specify a message_size to hash only the first 
`message_size` bytes of the input,

```rust
// example for hashing the first three elements of the input
Poseidon2::hash(input, 3);
```

The above example for Poseidon also includes Poseidon2.

## mimc_bn254 and mimc

`mimc_bn254` is `mimc`, but with hardcoded parameters for the BN254 curve. You can use it by
providing an array of Fields, and it returns a Field with the hash. You can use the `mimc` method if
you're willing to input your own constants:

```rust
fn mimc<N>(x: Field, k: Field, constants: [Field; N], exp : Field) -> Field
```

otherwise, use the `mimc_bn254` method:

```rust
fn mimc_bn254<N>(array: [Field; N]) -> Field
```

example:

```rust

fn main() {
    let x = [163, 117, 178, 149]; // some random bytes
    let hash = std::hash::mimc::mimc_bn254(x);
}
```

## hash_to_field

```rust
fn hash_to_field<N>(_input : [Field; N]) -> Field {}
```

Calculates the `blake2s` hash of the inputs and returns the hash modulo the field modulus to return
a value which can be represented as a `Field`.

